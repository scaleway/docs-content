---
meta:
  title: Creating a notification system with Scaleway SNS and Instances.
  description: This tutorial introduces you to using SNS for notifications via a simulated CPU monitoring example with terraform.
content:
  h1: Setting a notification system with terraform across instances using Scaleway's Instances and Messaging and Queuing SNS, through a simulated CPU monitoring example.
  paragraph: This tutorial walks you through creating a notification system between instances with terraform, from a simulated CPU monitoring webserver to a notification-receiving webserver.
categories:
  - messaging
  - instances
tags: messaging instances sns notifications monitoring terraform tutorial subscriber publisher receiver
dates:
  validation: 2024-03-08
  posted: 2024-03-08
---

## Introduction

<Macro id="iam-requirements" />

<Message type="requirement">
  - You have an account and are logged into the [Scaleway console](https://console.scaleway.com)
  - You have [set up authentication for the Terraform provider](https://registry.terraform.io/providers/scaleway/scaleway/latest/docs#authentication)
</Message>

This tutorial demonstrates setting up a notification system with terraform, where a simulated CPU usage threshold triggers alerts sent to an HTTP server using Scaleway's serverless products.
It involves two instances:
 - one simulates CPU load to trigger notifications, the publisher server,
 - the other serves as the receiver with an HTTP endpoint, the SNS topic's subscriber server.

<Lightbox src="publication.png" alt="" />

The focus of this tutorial is to set up SNS in an automated way, therefore all the infrastructure is handled with terraform.
To simplify this process, the web applications for each instance are downloaded through a docker image.
If you're interested by the code of the publisher and the subscriber servers, you can find the code on [GitHub](https://github.com/scaleway/sns-x-instances-tutorial).

# Create the terraform variable file

We will activate and SNS, create the two instances and link them with terraform.

## Create the base for the project sns_tutorial

1. Create a directory called `terraform`.
2. Inside this directory, create a file named `variables.tf`. We need a `public_ssh_key` to access the instances:
    ```
   variable "public_ssh_key" {
        description = "Public SSH Key to access your instances"
        type        = string
   }
    ```

3. Create the `main.tf` file still inside the `terraform` directory, and add the code below to set up the [Scaleway Terraform provider](https://registry.terraform.io/providers/scaleway/scaleway/latest/docs):
 ```
 terraform {
   required_providers {
     scaleway = {
       source = "scaleway/scaleway"
     }
   }
   required_version = ">= 0.13"
 }

 provider "scaleway" {
 }
```

4. Create the project `sns_tutorial` in your organization dashboard:
```
 resource "scaleway_account_project" "sns_tutorial" {
   name = "sns-tutorial"
 }
```

5. 1. Create the ssh key that will be used in `sns_tutorial` project to access the instances:

```
 resource "scaleway_iam_ssh_key" "main" {
   name        = "sns-tutorial-public-ssh-key"
   project_id = scaleway_account_project.sns_tutorial.id
   public_key = var.public_ssh_key
 }
```

## Create the SNS topic

1. Activate SNS in your project:

```
 resource "scaleway_mnq_sns" "main" {
   project_id = scaleway_account_project.sns_tutorial.id
   region = "fr-par" // the region is optional
 }
```

2. Create the credentials for SNS:
```
 resource "scaleway_mnq_sns_credentials" "main" {
   project_id = scaleway_account_project.sns_tutorial.id
   permissions {
     can_manage = true // to set up the topic subject, the subscription to the topic
     can_receive = true // to subscribe a topic
     can_publish = true // to publish messages to the topic
   }
   # TO WAIT THE ACTIVATION IS COMPLETED BEFORE CREATING THE CREDENTIALS
   depends_on = [
     scaleway_mnq_sns.main
   ]
 }
```
3. Finalise the SNS set up with creating the topic:
```
 resource "scaleway_mnq_sns_topic" "topic" {
   project_id = scaleway_account_project.sns_tutorial.id
   name = "sns-tutorial-topic"
   access_key = scaleway_mnq_sns_credentials.main.access_key
   secret_key = scaleway_mnq_sns_credentials.main.secret_key
 }
```


## Create the subscriber and publisher instances

We start to create the subscriber instance and server, and then we subscribes to the topic:
<Lightbox src="subscription.png" alt="" />

1. Create the security group that will serve both instances:
```
 resource "scaleway_instance_security_group" "sns_www" {
   project_id = scaleway_account_project.sns_tutorial.id
   inbound_default_policy  = "drop"
   outbound_default_policy = "accept"

   # FOR SSH CONNEXIONS
   inbound_rule {
     action = "accept"
     port = "22"
   }

   # FOR HTTP CONNEXIONS
   inbound_rule {
     action = "accept"
     port = "8081"
   }
 }
```

2. Create the subscriber server instance's ip address:
```
 resource "scaleway_instance_ip" "subscriber_public_ip" {
   project_id = scaleway_account_project.sns_tutorial.id
   zone = "fr-par-1"
 }
```

2. Create the subscriber instance server with all the tools needed at start-up:
```
 resource "scaleway_instance_server" "subscriber_sns_tuto_instance" {
   project_id = scaleway_account_project.sns_tutorial.id
   name = "suscriber-server"
   type  = "PLAY2-PICO"
   image = "debian_bookworm"
   # private_network {
   #pn_id = scaleway_vpc_private_network.sns_tutorial_pn.id
   #}
   ip_id = scaleway_instance_ip.subscriber_public_ip.id
   security_group_id= scaleway_instance_security_group.sns_www.id

   # USER DATA TO RUN THE SERVER AT START-UP
   user_data = {
     cloud-init = <<-EOF
     #cloud-config
     runcmd:
       - |
         apt-get update && apt-get install -y docker.io
         systemctl start docker
         systemctl enable docker
         docker pull rg.fr-par.scw.cloud/sns-x-instance-tutorial/subscriber-server:1.0
         docker run -d --restart=always \
           --name subscriber-server \
           -p 8081:8081 \
           rg.fr-par.scw.cloud/sns-x-instance-tutorial/subscriber-server:1.0
   EOF
   }

   # THE IP AND THE SECURITY GROUP MUST BE CREATED BEFOREHAND
   depends_on = [
     scaleway_instance_ip.subscriber_public_ip,
     scaleway_instance_security_group.sns_www
   ]
 }
```

3. Create a `terraform_data` ressource to wait for the port 8081 to be opened on the subscriber server before subscribe to the topic:
```
 resource "terraform_data" "bootstrap" {
   triggers_replace = [
     scaleway_instance_server.subscriber_sns_tuto_instance.id,
   ]

   # WAIT FOR 8081 PORT TO BE OPENED AND TIMEOUT OF 3 MINUTES
   provisioner "local-exec" {
     command = <<EOF
     #!/bin/bash
     TIMEOUT=180
     START_TIME=$(date +%s)
     while [ $(($(date +%s) - $START_TIME)) -lt $TIMEOUT ]; do
       nc -z ${scaleway_instance_server.subscriber_sns_tuto_instance.public_ip} 8081 && exit 0
      sleep 1
     done
     echo "Timeout reached"
     exit 1
     EOF
   }
 }
```
This will allow the subscriber server to receive immediately the confirmation link to subscribe to the topic, and not wait for the retry policy when you will want to confirm the subscription.
The confirmation step is be done manually for the purpose of the tutorial, but it can simply be automated with an HTTP client.

4. Create the SNS topic subscription with the subscriber server's endpoint:
```
 resource scaleway_mnq_sns_topic_subscription main {
   project_id = scaleway_account_project.sns_tutorial.id
   access_key = scaleway_mnq_sns_credentials.main.access_key
   secret_key = scaleway_mnq_sns_credentials.main.secret_key
   topic_id = scaleway_mnq_sns_topic.topic.id
   protocol = "http"
   endpoint = "http://${scaleway_instance_ip.subscriber_public_ip.address}:8081/notifications"

   depends_on = [
     terraform_data.bootstrap
   ]
 }
```

5. Create the publisher server instance's ip address:
```
 resource "scaleway_instance_ip" "publisher_public_ip" {
   project_id = scaleway_account_project.sns_tutorial.id
   zone = "fr-par-1"
 }
```

6. Create the instance itself with all the environment variables needed to publish the notifications on the topic:
```
 resource "scaleway_instance_server" "publisher_sns_tuto_instance" {
   project_id = scaleway_account_project.sns_tutorial.id
   name       = "publisher-server"
   type       = "PLAY2-PICO"
   image      = "debian_bookworm"
   ip_id      = scaleway_instance_ip.publisher_public_ip.id
   security_group_id= scaleway_instance_security_group.sns_www.id

   # USER DATA TO RUN THE SERVER AT START-UP WITH A SCRIPT TO SET THE SNS ENVIRONMENT VARIABLES RELATED TO THE TOPIC
   user_data = {
     cloud-init = <<-EOF
     #cloud-config
     write_files:
       - content: |
           #!/bin/bash
           export TOPIC_ARN="${scaleway_mnq_sns_topic.topic.arn}"
           export AWS_ACCESS_KEY="${scaleway_mnq_sns_credentials.main.access_key}"
           export AWS_SECRET_KEY="${scaleway_mnq_sns_credentials.main.secret_key}"
         owner: root:root
         path: /etc/profile.d/publisher-server_env.sh
         permissions: '0755'
     runcmd:
       - apt-get update && apt-get install -y docker.io
       - systemctl start docker
       - systemctl enable docker
       - chmod +x /etc/profile.d/publisher-server_env.sh
       # Using 'source' command doesn't work in cloud-init runcmd because each command runs in a separate shell.
       # this is why environment variables will be passed directly in the docker run command
       - docker pull rg.fr-par.scw.cloud/sns-x-instance-tutorial/publisher-server:1.0
       - |
         . /etc/profile.d/publisher-server_env.sh
         docker run -d --restart=always \
           --name publisher-server \
           -p 8081:8081 \
           -e TOPIC_ARN=$TOPIC_ARN \
           -e AWS_ACCESS_KEY=$AWS_ACCESS_KEY \
           -e AWS_SECRET_KEY=$AWS_SECRET_KEY \
           rg.fr-par.scw.cloud/sns-x-instance-tutorial/publisher-server:1.0
   EOF
   }

   # Depends on topic  creation and instance creation
   depends_on = [
     scaleway_mnq_sns_topic.topic,
     scaleway_instance_ip.publisher_public_ip,
     scaleway_instance_security_group.sns_www
   ]
 }
```

# Use the publisher and subscriber websites to test your setup

1. You need to confirm the subscription to the topic on the subscriber server. Go on the console, Instance, and copy the `subscriber_server` IP address.
2. Then go on the home page of your subscriber server: ```http://<your_subscriber_ip_address:8081```
3. Click on `Confirm subscription`
4. Once you've arrived to the AWS subscription confirmation page, go back to the home page, and click on `Notifications`
5. On the console, retrieve and copy the `publisher_server` IP address
6. Go on the home page of your subscriber server: ```http://<your_publisher_ip_address:8081```
7. Click on one or another CPU behavior, and check on the notification page of your subscriber server if the notification message appeared.

[//]: # (# To go further)